      SUBROUTINE PENMAN(SFCTMP,SFCPRS,SOLDN,LWDN,UWIND,VWIND,RH,ALBEDO,
     &                  Z,VEGTYPE,ETP)

      IMPLICIT NONE

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CC    PURPOSE:  TO CALCULATE POTENTIAL EVAPORATION FOR THE CURRENT POINT.
CC    =======   VARIOUS PARTIAL SUMS/PRODUCTS ARE ALSO CALCULATED AND
CC              PASSED BACK TO THE CALLING ROUTINE FOR LATER USE.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC


C     ALBEDO - COMPUTED ALBEDO WITH SNOWCOVER EFFECTS 
C     LWDN   - LW DOWNWARD RADIATION (W M-2; POSITIVE, IF DOWNWARD)
C     RH     - RELATIVE HUMIDITY (NO UNIT)
C     SFCTMP - AIR TEMPERATURE (K) AT HEIGHT Z ABOVE GROUND 
C     SFCPRS - PRESSURE AT HEIGHT Z ABOVE GROUND(PASCALS)
C     SOLDN  - SOLAR DOWNWARD RADIATION (W M-2; POSITIVE, IF DOWNWARD)
C             (COMPUTED USING ALB, SNOALB, SNEQV, AND SHDFAC->>veg greenness)
C     S      - SOIL HEAT FLUX (W M-2: NEGATIVE, IF DOWNWARD FROM SURFACE)
C     UWIND  - U COMPONENT OF WIND (M S-1)
C     VWIND  - V COMPONENT OF WIND (M S-1)
C     Z      - HEIGHT (M) ABOVE GROUND OF ATMOSPHERIC FORCING VARIABLES
C     Z0     - ROUGHNESS LENGTH (M)

      INTEGER VEGTYPE

      REAL ALBEDO
      REAL B
      REAL CP
      REAL DELTA
      REAL ETP
      REAL EV
      REAL EVSAT
      REAL FNET
      REAL FU
      REAL GAMMA
      REAL KAPPA
      REAL LHVP
      REAL LWDN
      REAL DVDM
      REAL RA
      REAL RH
      REAL RW
      REAL S
      REAL SFCPRS
      REAL SFCSPD
      REAL SFCTMP
      REAL SIGMA
      REAL SOLDN
      REAL T24
      REAL TMPC
      REAL UWIND
      REAL VWIND
      REAL Z
      REAL Z0
      REAL Z0_DATA(30)

C     CP     - HEAT CAPACITY (IN JOULES KG-1 K-1)
C     SIGMA  - STEFAN-BOLTZMANN CONSTANT (IN WATTS M-2 K-4)
C     KAPPA  - VON KARMAN CONSTANT
C     RA     - AIR DENSITY (IN G M-3)
C     RW     - WATER DENSITY (IN G M-3)

      PARAMETER (CP=1005.,KAPPA=0.4,SIGMA=5.67E-8, RW = 997.)

      DATA Z0_DATA /2.653, 0.826, 0.563, 1.089, 0.854, 0.856,
     *              0.035, 0.238, 0.065, 0.076, 0.011, 0.035,
     *              0.011, 0.000, 0.000, 0.000, 0.000, 0.000,
     *              0.000, 0.000, 0.000, 0.000, 0.000, 0.000,
     *              0.000, 0.000, 0.000, 0.000, 0.000, 0.000/

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     EXECUTABLE CODE BEGINS HERE...
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

C  INITIALIZAE  PARAMETERS Z0

      Z0 = Z0_DATA(VEGTYPE)

C  INITIALIZE GROUND HEAT FLUX S TO 0.0

      S = 0.0

C  COMPUTE A FEW VARIABLES FROM SFCTMP 

      TMPC = SFCTMP - 273.15
      T24 = SFCTMP * SFCTMP * SFCTMP * SFCTMP

C  CALCULATE DOWNWARD RADIATION - FNET (IN W M-2)

      FNET = SOLDN*(1.0-ALBEDO) + LWDN - SIGMA * T24 - S

C  CALCULATE LATENT HEAT OF VAPORIZATION - LHVP (IN JOULES KG-1)
C      LHVP = 597.3 - 0.564 * TMPC  (IN CAL G-1)

      LHVP = 2.501E+6 - 2370.0 * TMPC

C  CONVERT ENERGY UNITS [W/m**2] into WATER UNITS [m/s]
      FNET = FNET  / LHVP

C  CALCULATE PSYCHROMETRIC COEFFICIENT - GAMMA

      GAMMA = CP * SFCPRS / (0.622 * LHVP)

C  CALCULATE STATURATED VAPOR PRESSURE FOR AIR TEMPERATURE AT 
C     REFERENCE HEIGHT - EVSAT (IN PASCAL)

      EVSAT = 611. * EXP (17.27 * TMPC / (TMPC + 237.3))

C  CALCULATE VAPOR FOR AIR TEMPERATURE AT REFERENCE HEIGHT - EV (IN PASCAL)

      EV = EVSAT * RH

C  CALCULATE AIR DENSITY FROM VAPOR PRESSURE AND AIR TEMPERATURE RA (IN G M-3)

      RA = 2.17E+4 * EV / SFCTMP

C  CALCULATE DELTA - SLOPE OF THE RELATIONSHIP BETWEEN SATURATION 
C     VAPOR PRESSURE AND TEMPERATURE (IN PASCAL C-1)

      DELTA = 4098.0 * EVSAT / (TMPC + 237.3) ** 2

C  CALCULATE WIND SPEED - SFCSPD (IN M S-1)

      SFCSPD = SQRT (UWIND * UWIND + VWIND * VWIND)

C  CALCULATE EMPIRICAL WIND FUNCTION - FU IN M S-1

      FU = 0.26 * (1 + 0.54 * SFCSPD)

C  CALCULATE VAPOR TRANSFER COEEFICIENT B (IN M PASCAL-1 S-1)

      DVDM = 1.0
      B = DVDM * 0.622 * KAPPA**2 * RA * SFCSPD /
     &    ( SFCPRS * RW * (LOG (Z/Z0))**2 )

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C     FINISH PENMAN EQUATION CALCULATIONS.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

C  CALCULATE ETP USING EMPIRICAL WIND FUNCTION (IN M S-1)

      ETP = ( GAMMA * FU * (EVSAT - EV) / LHVP + FNET * DELTA ) / 
     &      ( DELTA + GAMMA )

C  CALCULATE ETP USING VAPOR TRANSFER COEFFICIENT (IN M S-1)

C      ETP = ( GAMMA * B * (EVSAT - EV) / LHVP + FNET * DELTA) /
C     &      ( DELTA + GAMMA )

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

      RETURN
      END
